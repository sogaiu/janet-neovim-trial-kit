# XXX: adjust as requirements change
(def [nvim-maj nvim-min] [0 10])

(defn ensure-bin
  [name]
  (try
    (with [f (file/temp)]
      (zero? (os/execute ["which" name] :px {:out f})))
    ([e]
      (eprintf "%s does not appear to be on PATH" name)
      false)))

(defn ensure-win-bin
  [name]
  (try
    (with [f (file/temp)]
      (zero? (os/execute ["where" name] :px {:out f})))
    ([e]
      (eprintf "%s does not appear to be on PATH" name)
      false)))

(defn get-nvim-info
  []
  (def out-fname "nvim.txt")
  (def cmd
    ["nvim"
     "-es"
     "-c" (string `redir! >` out-fname)
     "-c" `echo stdpath("data")`
     "-c" `echo stdpath("config")`
     "-c" `echo "\n"`
     "-c" `quit`])
  (var results nil)
  (try
    (when (zero? (os/execute cmd :px))
      (set results (slurp out-fname)))
    ([e]
      (eprintf "Failed to get nvim config info")))
  # clean up if needed
  (when (os/stat out-fname)
    (os/rm out-fname))
  #
  (if results
    (peg/match ~(sequence (any (set "\r\n"))
                          (some (sequence (not (choice "\r\n" "\n"))
                                          # target
                                          (capture (to (choice "\r\n" "\n")))
                                          (choice "\r\n" "\n"))))
               results)
    results))

(defn get-version
  [cmd ver-peg]
  (try
    (with [f (file/temp)]
      (def r (os/execute cmd :px {:out f}))
      (when (not (zero? r))
        (errorf "Non-zero exit code: %s" r))
      #
      (file/seek f :set 0)
      (def version-line (file/read f :line))
      (def version (peg/match ver-peg version-line))
      #
      version)
    ([e]
      (eprintf "Failed to determine version via %s" cmd)
      false)))

(defn have-nvim?
  []
  (if (= :windows (os/which))
    (when (not (ensure-win-bin "nvim.exe"))
      (eprintf "Failed to find nvim.exe on PATH\n")
      (break false))
    (when (not (ensure-bin "nvim"))
      (eprintf "Failed to find nvim on PATH\n")
      (break false)))
  (def ver-str
    (first
      (get-version ["nvim" "--version"]
                   ~(sequence "NVIM v"
                              (capture (to (choice "\r\n" "\n" -1)))))))
  (when (not ver-str)
    (eprintf "Failed to determine nvim version\n")
    (break false))
  #
  ver-str)

(defn parse-nvim-ver-str
  [ver-str]
  (peg/match ~(sequence (capture :d+)
                        "."
                        (capture :d+)
                        "."
                        (capture :d+)
                        (capture (to -1)))
             ver-str))

(defn nvim-sufficient?
  [ver-str]
  (if-let [[maj min patch other] (parse-nvim-ver-str ver-str)]
    (and (= (scan-number maj) nvim-maj)
         (>= (scan-number min) nvim-min))
    (do
      (eprintf "Failed to parse nvim version string: %s" ver-str)
      false)))

(defn have-cc?
  []
  (if (= :windows (os/which))
    (when (and (not (ensure-win-bin "gcc.exe"))
               (not (ensure-win-bin "clang.exe")))
      (eprintf "Failed to find gcc.exe or clang.exe on PATH\n")
      (break false))
    (when (not (ensure-bin "cc"))
      (eprintf "Failed to find C compiler (cc) on PATH\n")
      (break false)))
  #
  true)

# XXX: intent is to modify what neovim's stdpath returns
#      see :h standard-path and :h base-directories
(defn isolate
  [root-dir]
  (os/setenv "XDG_CONFIG_HOME" (string root-dir "/config"))
  (os/setenv "XDG_DATA_HOME" (string root-dir "/data"))
  #(os/setenv "XDG_RUNTIME_DIR" (string root-dir "/runtime"))
  (os/setenv "XDG_STATE_HOME" (string root-dir "/state"))
  (os/setenv "XDG_CACHE_HOME" (string root-dir "/cache"))
  (os/setenv "XDG_LOG_FILE" (string root-dir "/log")))

(defn setup
  []
  # verify nvim exists and its version is sufficient
  (def nvim-ver-str (have-nvim?))
  (when (not nvim-ver-str)
    (os/exit 1))
  (when (not (nvim-sufficient? nvim-ver-str))
    (eprintf "nvim version is %s, need at least %d.%d.0"
             nvim-ver-str nvim-maj nvim-min)
    (os/exit 1))
  # ensure appropriate c compiler available
  (when (not have-cc?)
    (os/exit 1))
  # figure out where various files should live
  (def nvim-info (get-nvim-info))
  (when (not nvim-info)
    (eprintf "Failed to determine nvim config and data locations")
    (os/exit 1))
  (def [nvim-data nvim-config] nvim-info)
  # copy relevant files to appropriate locations
  (os/mkdir nvim-data)
  (os/mkdir (string nvim-data "/site"))
  (os/mkdir (string nvim-data "/site/autoload"))
  (spit (string nvim-data "/site/autoload/plug.vim")
        (slurp "plug.vim"))
  #
  (os/mkdir nvim-config)
  (spit (string nvim-config "/init.vim")
        (slurp "init.vim"))
  # install plugins
  (print "Installing plugins...")
  (os/execute ["nvim"
               "-es"
               "-u" "install.vim"
               "-c" "PlugInstall"
               "-c" "quit"]
              :px))

(defn main
  [& argv]
  (def root-dir (os/cwd))
  (isolate root-dir)
  (setup)
  (os/execute ["nvim" ;(array/slice argv 1)] :px))

